CMAKE_MINIMUM_REQUIRED(VERSION 3.12)

FILE(READ "VERSION" AVEO_VERSION)
STRING(STRIP ${AVEO_VERSION} AVEO_VERSION)
PROJECT(AVEO VERSION ${AVEO_VERSION} LANGUAGES C CXX)

## CMake Stuff -----------------------------------------------------------------
IF(NOT ${CMAKE_HOST_SYSTEM_NAME} MATCHES "Linux")
	MESSAGE(FATAL "Not supported operating system. Only Linux supported!")
ENDIF()

IF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
	GET_FILENAME_COMPONENT(INSTALL_PREFIX "/usr/local/ve/aveo-${PROJECT_VERSION}/" ABSOLUTE)
	SET(CMAKE_INSTALL_PREFIX ${INSTALL_PREFIX} CACHE PATH "CMAKE install path" FORCE)
ENDIF()

SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
SET(CMAKE_INSTALL_RPATH "\$ORIGIN")

# VE-urpc ----------------------------------------------------------------------
OPTION(WITH_DOWNLOAD_DEPENDENCIES "Automatically download dependencies" ON)
IF(WITH_DOWNLOAD_DEPENDENCIES)
	SET(EXT_VEURPC_DIR "${CMAKE_CURRENT_BINARY_DIR}/ve-urpc")
	SET(VEURPC_DIR ${EXT_VEURPC_DIR}/install)

	INCLUDE(ExternalProject)
	ExternalProject_Add(ve-urpc
		BINARY_DIR			${EXT_VEURPC_DIR}/build
		GIT_REPOSITORY		https://github.com/mergian/ve-urpc
		GIT_SHALLOW			TRUE
		BUILD_COMMAND		make -j
		CONFIGURE_COMMAND	${CMAKE_COMMAND} -DCMAKE_INSTALL_PREFIX=${VEURPC_DIR} ${EXT_VEURPC_DIR}/src
		INSTALL_COMMAND		make install -j
		PATCH_COMMAND		""
		PREFIX				${EXT_VEURPC_DIR}
		SOURCE_DIR			${EXT_VEURPC_DIR}/src
		STAMP_DIR			${EXT_VEURPC_DIR}/stamp
		TMP_DIR				${EXT_VEURPC_DIR}/tmp
	)

	SET(VEURPC_INCLUDE			"${VEURPC_DIR}/include")
	SET(VEURPC_HOST_SHARED		"${VEURPC_DIR}/lib/liburpcVH.so")
	SET(VEURPC_HOST_STATIC		"${VEURPC_DIR}/lib/liburpcVH.a")
	SET(VEURPC_DEVICE_SHARED	"${VEURPC_DIR}/lib/liburpcVE.so")
	SET(VEURPC_DEVICE_STATIC	"${VEURPC_DIR}/lib/liburpcVE.a")
	SET(VEURPC_OMP_SHARED		"${VEURPC_DIR}/lib/liburpcVE.so")
	SET(VEURPC_OMP_STATIC		"${VEURPC_DIR}/lib/liburpcVE.a")
	INSTALL(FILES
		${VEURPC_HOST_SHARED}
		${VEURPC_HOST_STATIC}
		${VEURPC_OMP_SHARED}
		${VEURPC_OMP_STATIC}
		DESTINATION lib
	)
ELSE()
	SET(VEURPC_DIR "/usr/local/ve/ve-urpc/cmake")
	FIND_PACKAGE(VEURPC)
ENDIF()

INCLUDE_DIRECTORIES(${VEURPC_INCLUDE})

# AVEO -------------------------------------------------------------------------
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_LIST_DIR}/src/)

ADD_LINK_OPTIONS(-Wl,--no-as-needed -Wl,-z,defs)
ADD_SUBDIRECTORY(scripts)
ADD_SUBDIRECTORY(src)

IF(WITH_DOWNLOAD_DEPENDENCIES)
	ADD_DEPENDENCIES(aveoVH ve-urpc)
	ADD_DEPENDENCIES(aveoVH_static ve-urpc)
	ADD_DEPENDENCIES(aveoVE ve-urpc)
	ADD_DEPENDENCIES(aveoVE_static ve-urpc)
	ADD_DEPENDENCIES(aveorun ve-urpc)
	ADD_DEPENDENCIES(aveorun-ftrace ve-urpc)
ENDIF()

ADD_SUBDIRECTORY(test)